// Plugins ---------------------------------
plugins {
    id 'application'
    id 'java'
    id 'idea'
    id 'checkstyle'
    id 'jacoco'
    id 'com.adarshr.test-logger' version '1.1.2'
}

// Repos -----------------------------------
repositories {
    jcenter()
}

// Dependencies ----------------------------
dependencies {
    testCompile 'junit:junit:4.12'
    compile 'junit:junit:4.12'
}

// Project Settings-------------------------
group = 'Bordeaux Ink.'
version = rootProject.file('VERSION').text.trim()
sourceCompatibility = 1.8
project.buildDir = 'app/build/'
compileJava.options.fork = true
mainClassName = 'tech.subluminal.main.Subluminal'

def description = """
    Project: ${project.name}-
    
    Author(s):  Rik de Graaff,
                Lucas Galery KÃ¤ser,
                Luc Kury,
                David Lengweiler
"""

checkstyle { 
   toolVersion = '8.5'
}

// Source Sets -------------------------------
sourceSets {
    main {
         java {
              srcDir 'app/src/main/java'
              }
         java.outputDir = file('app/out/main')
         }

    test {
         java {
              srcDir 'app/src/test/java'
              }
         java.outputDir = file('app/out/test')
         }
}

idea {
    module{
        testSourceDirs += project.sourceSets.main.java.srcDirs
        inheritOutputDirs = true
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

// Jar file settings -----------------------
jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest{
        attributes 'Main-Class': 'tech.subluminal.main.Subluminal'
    }
}

// Define new tasks -------------------------
task info {
    doFirst{
        println "${description}"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

task runServer(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  main = "tech.subluminal.main.Subluminal"
  args "server", "1729"
  standardInput = System.in
}

task runClient(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  main = "tech.subluminal.main.Subluminal"
  args "client", "localhost:1729"
  standardInput = System.in
}

task newTestEnv(type: Exec) {
    workingDir 'app/build/libs/'
    commandLine 'java', '-jar', 'Subluminal-3.0.0.jar', 'server', '1790'
}

task runTestEnv(type: Exec) {
    workingDir 'app/build/libs/'
    commandLine 'java', '-jar', 'Subluminal-3.0.0.jar', 'client', 'localhost:1790'
}

// Task settings ---------------------------
javadoc {
    setDestinationDir(file("docs/javadoc"))
}

test {
    reports.html.destination file("$buildDir/reports/test")
}

jacocoTestReport {
    reports.html.destination file("$buildDir/reports/coverage")
}

run {
    standardInput = System.in
}

// task openBrowser(type:Exec) {
//     java.awt.Desktop.desktop.browse rootProject.path+"docs/javadocs/index.html".toURI()
// }